
SOUND	GROUP	0


;SAMPLE DATA IS ASSUMED TO CONSIST OF THE FOLLOWING :-
;5 BYTES OF SOD DATA (NOT INCLUDING SAMPLE NUMBER)
;1 WORD LOOP OFFSET
;1 WORD SIZE
;SAMPLE DATA

	ON816

	ORG	$0000

SMPLLST	DW	0
APU0DAT	DB	0
SMPLSRC	DW	0
	DB	0
SODCNT	DB	0
SODTRG	DW	0
WAVETRG	DW	0
SMPLTRG	DW	0

SOD	EQU	$0000
WAVE	EQU	$0000
SAMPLE	EQU	$0000

APU0	EQU	$2140
APU1	EQU	$2141
APU2	EQU	$2142
APU3	EQU	$2143

	ORG	$8000

;SOUND DOWNLOAD ROUTINE
;USES STANDARD TRANSMISSION METHOD

;ON ENTRY :-

;ACC = GROUP NUMBER TO DOWNLOAD

DOWNSND	ON16A
	ON16I
	REP	#$30
	ASL	A
	TAX
	LDA	SLISTA,X
	STA	.SMPLLST
	LDA	#SOD
	STA	.SODTRG
	LDA	#WAVE
	STA	.WAVETRG
	LDA	#SAMPLE
	STA	.SMPLTRG
	OFF16A
	SEP	#$20
	LDA	#$CC
	STA	.APU0DAT
	ON16A
	REP	#$20
	LDA	#$BBAA
_WAIT	CMP	APU0
	BNE	_WAIT
_NEXT	LDY	.SMPLLST
	LDA	SLISTS,Y
	OFF16A
	SEP	#$20
	BPL	_MORE
	LDA	#$00
	STA	APU1
	ON16A
	REP	#$20
	LDA	SLISTS+2,Y
	STA	APU2
	OFF16A
	SEP	#$20
	LDA	APU0DAT
	STA	APU0
_RUN1	CMP	APU0
	BNE	_RUN1
	ON16A
	REP	#$20
	RTS
	OFF16A
_MORE	STA	.SMPLSRC+2
	ON16A
	REP	#$20
	LDA	SLISTS+2,Y
	STA	.SMPLSRC
	TYA
	CLC
	ADC	#$0004
	STA	.SMPLLST
	LDY	#$0000
	OFF16A
	SEP	#$20
	LDA	#$01
	STA	APU1
	ON16A
	REP	#$20
	LDA	.SODTRG
	STA	APU2
	CLC
	ADC	#$0006
	STA	.SODTRG
	OFF16A
	SEP	#$20
	LDA	.APU0DAT
	STA	APU0
_SOD1	CMP	APU0
	BNE	_SOD1
	LDA	.SODCNT
	STA	APU1
	LDA	#$00
	STA	APU0
	INC	A
	LDX	#$0005
_SOD2	XBA
	LDA	[.SMPLSRC],Y
	STA	APU1
	INY
	XBA
	STA	APU0
_SOD3	CMP	APU0
	BNE	_SOD3
	INC	A
	DEX
	BNE	_SOD2
_SOD4	INC	A
	BEQ	_SOD4
	STA	.APU0DAT
	STA	APU1
	ON16A
	REP	#$20
	LDA	.WAVETRG
	STA	APU2
	CLC
	ADC	#$0004
	STA	.WAVETRG
	OFF16A
	SEP	#$20
	LDA	.APU0DAT
	STA	APU0
_WAVE1	CMP	APU0
	BNE	_WAVE1
	ON16A
	REP	#$20
	LDA	.SMPLTRG
	OFF16A
	SEP	#$20
	STA	APU1
	LDA	#$00
	STA	APU0
_WAVE2	CMP	APU0
	BNE	_WAVE2
	XBA
	STA	APU1
	LDA	#$01
	STA	APU0
_WAVE3	CMP	APU0
	BNE	_WAVE3
	ON16A
	REP	#$20
	LDA	[.SMPLSRC],Y
	CLC
	ADC	.SMPLTRG
	INY
	INY
	OFF16A
	SEP	#$20
	STA	APU1
	LDA	#$02
	STA	APU0
_WAVE4	CMP	APU0
	BNE	_WAVE4
	XBA
	STA	APU1
	LDA	#$03
	STA	APU0
_WAVE5	CMP	APU0
	BNE	_WAVE5
	STA	APU1
	ON16A
	REP	#$20
	LDA	.SMPLTRG
	STA	APU2
	LDA	[.SMPLSRC],Y
	TAX
	CLC
	ADC	.SMPLTRG
	STA	.SMPLTRG
	OFF16A
	SEP	#$20
	LDA	#$05
	STA	APU0
_SMPL1	CMP	APU0
	BNE	_SMPL1
	LDA	#$00
_SMPL2	XBA
	LDA	[.SMPLSRC],Y
	STA	APU1
	INY
	XBA
	STA	APU0
_SMPL3	CMP	APU0
	BNE	_SMPL3
	INC	A
	DEX
	BNE	_SMPL2
_SMPL4	INC	A
	BEQ	_SMPL4
	STA	.APU0DAT
	ON16A
	REP	#$20
	BRL	_NEXT

;OFFSETS OF SAMPLE LISTS

SLISTA	DW	SLIST1-SLISTS

;SAMPLE BANK, SAMPLE DATA ADDRESS
;IF BANK -VE THEN NEXT WORD RUN ADDRESS

SLISTS
SLIST1	DW	0,0
